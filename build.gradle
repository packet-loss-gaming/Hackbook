plugins {
    id 'java'
    id 'maven-publish'
    id 'io.papermc.paperweight.userdev' version '1.3.8'
}

repositories {
    mavenLocal()

    maven {
        name 'Packet Loss Gaming Repository'
        url 'https://mvn.packetloss.gg/'
    }
    maven {
        name 'Paper Repository'
        url 'https://papermc.io/repo/repository/maven-public/'
    }
}

dependencies {
    paperweightDevelopmentBundle 'io.papermc.paper:dev-bundle:1.19.2-R0.1-SNAPSHOT'
}

group = 'gg.packetloss'
version = '1.19.2.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

// Butcher the dev jar so as to publish a properly obfuscated jar
def javaComponent = components.java as AdhocComponentWithVariants
javaComponent.withVariantsFromConfiguration(configurations.apiElements) {
    skip()
}
javaComponent.withVariantsFromConfiguration(configurations.runtimeElements) {
    skip()
}
javaComponent.addVariantsFromConfiguration(configurations.reobf) {
    mapToMavenScope("compile")
}

publishing {
    publications {
        maven(MavenPublication) {
            from javaComponent
        }
    }

    repositories {
        maven {
            url = System.getProperty("deploy-url")
            credentials {
                username = System.getProperty("deploy-username")
                password = System.getProperty("deploy-password")
            }
        }
    }
}
